plugins {
    id "com.android.application"
    id "org.jetbrains.kotlin.android"
    id "dev.flutter.flutter-gradle-plugin"
}

def localProperties = new Properties()
def localPropertiesFile = rootProject.file('local.properties')
if (localPropertiesFile.exists()) {
    localPropertiesFile.withReader('UTF-8') { reader ->
        localProperties.load(reader)
    }
}

def flutterVersionCode = localProperties.getProperty('flutter.versionCode')
if (flutterVersionCode == null) {
    flutterVersionCode = '1'
}

def flutterVersionName = localProperties.getProperty('flutter.versionName')
if (flutterVersionName == null) {
    flutterVersionName = '1.0'
}

def keystorePropertiesFile = rootProject.file("key.properties")
def keystoreProperties = new Properties()
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

kotlin {
    jvmToolchain 17
}

android {
    namespace "com.mateusrodcosta.apps.vidyamusic"
    compileSdk 34

    defaultConfig {
        applicationId "com.mateusrodcosta.apps.vidyamusic"
        minSdkVersion 26
        targetSdkVersion 34
        versionCode flutterVersionCode.toInteger()
        versionName flutterVersionName
    }

    signingConfigs {
        release {
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
            storeFile file(keystoreProperties['storeFile'])
            storePassword keystoreProperties['storePassword']

            // Always enable v2 and v3 signing schemes, which will be used on modern Android OSes
            enableV2Signing true
            enableV3Signing true
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
            manifestPlaceholders = [appName: "Vidya Music"]
        }
        debug {
            applicationIdSuffix '.debug'
            versionNameSuffix '-debug'
            manifestPlaceholders = [appName: "Vidya Music (Debug)"]
        }
        profile {
            applicationIdSuffix '.profile'
            versionNameSuffix '-profile'
            manifestPlaceholders = [appName: "Vidya Music (Profile)"]
        }
    }

    splits {
        abi {
            enable true

            reset()
            //noinspection ChromeOsAbiSupport
            include "armeabi-v7a", "arm64-v8a", "x86_64"

            universalApk true
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    kotlinOptions {
        jvmTarget = '17'
    }
}

flutter {
    source '../..'
}

dependencies {}

ext.abiCodes = ['armeabi-v7a': 1, 'arm64-v8a': 2, x86_64: 4]

android.applicationVariants.configureEach { variant ->

    variant.outputs.each { output ->

        def baseAbiVersionCode =
                project.ext.abiCodes.get(output.getFilter("ABI"))

        if (baseAbiVersionCode != null) {

            output.versionCodeOverride =
                // As required by F-Droid, version code at beginning and abi code at the end
                // If wanting to build a universal APK with similar naming scheme, do so manually
                // via `--build-number` argument from `flutter build apk`
                variant.versionCode * 100 + baseAbiVersionCode
                // Default split apk version code, api code at beginning and version code at the end
                //baseAbiVersionCode * 1000 + variant.versionCode
        }
    }
}

